import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.0.1'
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/central/' }
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

group 'com.anyilanxin.plugin'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

// Read more: https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-migration.html
intellijPlatform {
    pluginConfiguration {
        name = "${pluginName}"
        version = "${pluginVersion}"
    }
    projectName = "${pluginName}"
    buildSearchableOptions = false
    sandboxContainer = file("${rootProject.rootDir}/idea-sandbox")
}


dependencies {
    intellijPlatform {
        intellijIdeaUltimate("2024.2")
        pluginVerifier()
        instrumentationTools()
        javaCompiler()
        bundledPlugin("com.intellij.database")
    }
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

    def lombok = "1.18.32"
    compileOnly "org.projectlombok:lombok:${lombok}"
    annotationProcessor "org.projectlombok:lombok:${lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok}"

    testImplementation 'junit:junit:4.13.2'
}


processResources {
    filteringCharset = 'UTF-8'
    include('**/*.*')
    filesMatching('plugin.xml') {
        filter(ReplaceTokens, tokens: ['version': version, 'pluginName': pluginName])
    }
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}
