import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.4'
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/central/' }
    mavenCentral()
}

group 'com.anyilanxin.plugin'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

// Read more: https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html
intellij {
    pluginName = "${pluginName}"
    version = "2024.1"
    type = 'IU'
    sandboxDir = "${rootProject.rootDir}/idea-sandbox"
    downloadSources = true
    updateSinceUntilBuild = false
    instrumentCode = true
    plugins = ['com.intellij.java', 'com.intellij.database']
}


dependencies {
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

    def lombok = "1.18.32"
    compileOnly "org.projectlombok:lombok:${lombok}"
    annotationProcessor "org.projectlombok:lombok:${lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok}"

    testImplementation 'junit:junit:4.13.2'
}


processResources {
    filteringCharset = 'UTF-8'
    include('**/*.*')
    filesMatching('plugin.xml') {
        filter(ReplaceTokens, tokens: ['version': version])
    }
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}
